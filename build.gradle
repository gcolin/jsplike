buildscript {
    repositories {
        mavenLocal()
        maven {
            url "http://repo.test/simple-repo/repository/public"
        }
        maven {
            url "http://gcolin.net/public"
        }
    }
    dependencies {
        classpath 'net.gcolin:parent:1.0'
    }
}

description = """Enhanced JSP that compile EL expressions"""

allprojects {

    apply plugin: "net.gcolin.parent"

    group = 'net.gcolin'
    version = '1.0'

    dependencies {
	compile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
	compile group: 'javax.el', name: 'javax.el-api', version:'3.0.0'
	compile group: 'javax.servlet.jsp', name: 'javax.servlet.jsp-api', version:'2.3.1'
	compile group: 'net.gcolin', name: 'common', version:'1.0'
	testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
	testCompile group: 'jstl', name: 'jstl', version:'1.2'
	testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version:'2.4.7'
    }
}

project(':jsplike-ecj') {
	description = """Compile with the Eclipse Java Compiler"""

	dependencies {
		compile rootProject
		compile group: 'org.eclipse.jdt.core.compiler', name: 'ecj', version:'4.5.1'
	}
}

project(':jsplike-javac') {
	description = """Compile with Javac command"""

	dependencies {
		compile rootProject
	}
}

project(':war-optimizer') {
	description = """Optimize a war or a jar"""

	dependencies {
		compile rootProject
		compile group: 'org.jsoup', name: 'jsoup', version:'1.8.3'
		compile group: 'com.google.javascript', name: 'closure-compiler', version:'v20150729'
	}
}

project(':war-optimizer-gradle-plugin') {
	apply plugin: 'groovy'

	description = """Optimize a war or a jar"""

	dependencies {
		compile project(':war-optimizer')
		compile project(':jsplike-javac')
		compile gradleApi()
    	compile localGroovy()
	}
}

allprojects {
	
    install.repositories.mavenInstaller.pom.project {
        inceptionYear '2015'
        description description
    }

    uploadArchives.repositories.mavenDeployer.pom.project {
        inceptionYear '2015'
        description description
    }
}
