plugins {
	id "org.nosphere.apache.rat" version "0.2.2"
	id "com.github.hierynomus.license" version "0.13.1"
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'checkstyle'

license {
    header rootProject.file('config/HEADER')
    exclude '**/*.properties'
    exclude '**/*.txt'
    exclude '**/*.xml'
    exclude '**/*.jsp'
    exclude '**/META-INF/services/**'
}

install.repositories.mavenInstaller.pom.project {
    inceptionYear '2015'
	developers {
		developer {
			name "Gael COLIN"
		}
	}
    licenses {
       license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'manual'
       }
    }
}

group = 'net.gcolin'
version = '1.0'

description = """Enhanced JSP that compile EL expressions"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

tasks.withType(FindBugs) {
	excludeFilter new File("FindBugsFilter.xml")
    reports {
        xml.enabled false
        html.enabled true
    }
}

checkstyle {
    toolVersion = "7.1.1"
}

rat {

    excludes = [ '**/build/**','**/bin/**','**/.gradle/**','**/.classpath','**/.project',,'**/.settings/**',
    '**/*.iml', '**/*.ipr','**/*.iws','gradle.properties','gradlew','gradlew.bat', '**/target/**',
    '**/gradle/**','README.md','settings.gradle','build.gradle','FindBugsFilter.xml','**/config/**',
    '**/*.jsp', '**/*.xml', '**/*.txt', '**/*.properties','**/META-INF/**', '**/*.html', '**/*.js' ]

}

repositories {
    mavenLocal()
    mavenCentral()
}
dependencies {
    compile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.1.0'
    compile group: 'javax.el', name: 'javax.el-api', version:'3.0.0'
    compile group: 'javax.servlet.jsp', name: 'javax.servlet.jsp-api', version:'2.3.1'
    compile group: 'net.gcolin', name: 'common', version:'1.0'
    testCompile group: 'junit', name: 'junit', version:'4.11'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version:'1.7.21'
    testCompile group: 'jstl', name: 'jstl', version:'1.2'
    testCompile group: 'org.codehaus.groovy', name: 'groovy-all', version:'2.4.7'
}
